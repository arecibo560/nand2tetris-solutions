// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:

	/* 
	 * Determine if zeroing is needed
	 */

	Mux16(a=x, b=false, sel=zx, out=zxOut);
	Mux16(a=y, b=false, sel=zy, out=zyOut);

	/*
	 * Determine if Not-ing is necessary
	 */
	
	Not16(in=zxOut, out=notzxOut);
	Not16(in=zyOut, out=notzyOut);

	Mux16(a=zxOut, b=notzxOut, sel=nx, out=xx);
	Mux16(a=zyOut, b=notzyOut, sel=ny, out=yy);


	/*
	 * Determine ADD or AND
	 */

	And16(a=xx, b=yy, out=andXY);
	Add16(a=xx, b=yy, out=sumXY);
	
	Mux16(a=andXY, b=sumXY, sel=f, out=fout);

	/*
	 * Determine out
	 */

	Not16(in=fout, out=notFout);

	//first8 and second8 are for Or-ing in zr computation
	//the 16th bit determines if the number is negative
	Mux16(a=fout, b=notFout, sel=no, out=out, 
					out[0..7]=first8, 
					out[8..15]=second8, 
					out[15]=ng);
	
	/*
	 * Determine if out is zero
	 */
	
	//would be usefull if I had a Or16Way...
	Or8Way(in=first8, out=zr1);
	Or8Way(in=second8, out=zr2);
	Or(a=zr1, b=zr2, out=zr3);
	Not(in=zr3, out=zr);

	
}
